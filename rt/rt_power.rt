
    .file   "rt_power.rt"
    .text
    .globl  rt_power
    .type   rt_power, @function
rt_power:
    mov $1, %rax    # default return 1 (i.e. n ^ 0 and a few others)
    test %rsi, %rsi
    je done
    js negative
    shr %rsi
    jnc even
    mov %rdi, %rax
even:
    je done
    imul %rdi, %rdi
    mov %rdi, %rdx  # mulx uses %rdx as one of the sources
repeat:
    shr %rsi
    jnc zero_bit
    mulx %rax, %rax, %rcx   # %rcx:%rax = %rax * %rdx (%rcx is throw away)
zero_bit:
    je done
    mulx %rdx, %rdx, %rcx   # %rcx:%rdx = %rdx * %rdx (%rcx is throw away)
    jmp repeat

negative:
    cmp $1, %rdi     # 1 / 1^n = 1
    je done
    cmp $-1, %rdi
    je signed
    xor %eax, %eax   # 1 / (x ^ n) is 0 since integer division has not decimal digits
    ret

signed:
    shr %rsi
    jnc done
    mov %rdi, %rax  # return -1 (1 / -1 = -1)
done:
    ret

// vim: ts=4 sw=4 et syntax=asm
